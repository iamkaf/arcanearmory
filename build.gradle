plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

	// REI
	maven { url "https://maven.shedaniel.me" }

	// owo
	maven { url 'https://maven.wispforest.io' }

	// modmenu, trinkets
	maven { url "https://maven.terraformersmc.com/" }

	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}

	maven {url = 'https://jitpack.io'}

	// magna
	maven { url "https://maven.draylar.dev/releases" }
}

fabricApi {
	configureDataGeneration()
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:12.0.684"
	modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:12.0.684"

	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	// for hammers
	include "dev.draylar:magna:${project.magna_version}"
	modImplementation ("dev.draylar:magna:${project.magna_version}") {
		exclude group: "net.fabricmc"
		exclude group: "com.jamieswhiteshirt"
		exclude group: "me.shedaniel.cloth"
	}

	// for bows
	modImplementation "maven.modrinth:projectile-damage-attribute:${project.projectile_damage_version}-fabric"
	include "maven.modrinth:ranged-weapon-api:${project.ranged_weapon_api_version}"
	modImplementation "maven.modrinth:ranged-weapon-api:${project.ranged_weapon_api_version}"

	modRuntimeOnly "com.terraformersmc:modmenu:${project.modmenu_version}"

	modRuntimeOnly "io.wispforest:owo-lib:${project.owo_version}"

	// alloy forgery
	modRuntimeOnly "maven.modrinth:jhl28YkY:2.1.2+1.20" // <-- do you know how to fix this? pls help :3

	// shields
	include modImplementation("com.github.CrimsonDawn45:Fabric-Shield-Lib:v${project.fabric_shield_lib_version}")
	modImplementation "maven.modrinth:midnightlib:${project.midnightlib_version}" // required for shields
	modImplementation "com.github.Chocohead:Fabric-ASM:v${project.fabricasm_version}" // required for shields

	// Uncomment the following line to enable the deprecated Fabric API modules.
	// These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.

	// modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}